import org.gradle.internal.os.OperatingSystem



plugins {
    id 'application'
}

group 'fori.systems'
version '0.0.1'

repositories {
    mavenCentral()
}
sourceSets {
    main {
        java {
            srcDirs = ['src']
        }
    }
}
jar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE


    from {
        configurations.runtimeClasspath.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }

    manifest {
        manifest.attributes["Main-Class"] = "whisper.Launcher"
    }

}


switch (OperatingSystem.current()) {
    case OperatingSystem.LINUX:
        project.ext.lwjglNatives = "natives-linux"
        def osArch = System.getProperty("os.arch")
        if (osArch.startsWith("arm") || osArch.startsWith("aarch64")) {
            project.ext.lwjglNatives += osArch.contains("64") || osArch.startsWith("armv8") ? "-arm64" : "-arm32"
        } else if  (osArch.startsWith("ppc")) {
            project.ext.lwjglNatives += "-ppc64le"
        } else if  (osArch.startsWith("riscv")) {
            project.ext.lwjglNatives += "-riscv64"
        }
        break
    case OperatingSystem.MAC_OS:
        project.ext.lwjglNatives = System.getProperty("os.arch").startsWith("aarch64") ? "natives-macos-arm64" : "natives-macos"
        break
    case OperatingSystem.WINDOWS:
        def osArch = System.getProperty("os.arch")
        project.ext.lwjglNatives = osArch.contains("64")
                ? "natives-windows${osArch.startsWith("aarch64") ? "-arm64" : ""}"
                : "natives-windows-x86"
        break
}

project.ext.lwjglVersion = "3.3.6"
project.ext.jomlVersion = "1.10.8"

dependencies {

    implementation 'com.esotericsoftware:kryo:5.6.0'
    implementation group: 'commons-cli', name: 'commons-cli', version: '1.9.0'
    implementation platform("org.lwjgl:lwjgl-bom:$lwjglVersion")
    implementation "org.lwjgl:lwjgl-assimp"
    implementation "org.lwjgl:lwjgl"
    implementation "org.lwjgl:lwjgl-glfw"
    implementation "org.lwjgl:lwjgl-stb"
    implementation "org.lwjgl:lwjgl-vma"
    implementation "org.lwjgl:lwjgl-vulkan"
    implementation "org.lwjgl:lwjgl-spvc"


    implementation 'dev.dominion.ecs:dominion-ecs-engine:0.9.0'



    runtimeOnly "org.lwjgl:lwjgl::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-glfw::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-stb::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-vma::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-assimp::$lwjglNatives"
    implementation "org.lwjgl:lwjgl-spvc::$lwjglNatives"


    // java bindings
    implementation("de.fabmax:physx-jni:2.6.0")

    // native libraries - you can add the one matching your system or all
    runtimeOnly("de.fabmax:physx-jni:2.6.0:natives-windows")
    runtimeOnly("de.fabmax:physx-jni:2.6.0:natives-linux")
    runtimeOnly("de.fabmax:physx-jni:2.6.0:natives-macos")
    runtimeOnly("de.fabmax:physx-jni:2.6.0:natives-macos-arm64")


    if (lwjglNatives == "natives-macos" || lwjglNatives == "natives-macos-arm64") runtimeOnly "org.lwjgl:lwjgl-vulkan::$lwjglNatives"
    implementation "org.joml:joml:${jomlVersion}"
}


